import linear_algebra.matrix.pos_def

namespace finset
variables {M Î¹ : Type*} [ordered_cancel_comm_monoid M] {f g : Î¹ â†’ M} {s t : finset Î¹}
open_locale big_operators

--TODO: move
@[to_additive sum_pos'] lemma one_lt_prod' (Hle : âˆ€ i âˆˆ s, 1 â‰¤ f i) (Hlt : âˆƒ i âˆˆ s, 1 < f i) :
  1 < (âˆ i in s, f i) :=
lt_of_le_of_lt (by rw prod_const_one) $ prod_lt_prod' Hle Hlt

end finset

namespace matrix
variables {ð•œ : Type*} [is_R_or_C ð•œ] {m n : Type*} [fintype m] [fintype n]
open_locale matrix

/-- A matrix `M : matrix n n ð•œ` is positive semidefinite if it is hermitian
   and `xá´´Mx` is nonnegative for all `x`. -/
def pos_semidef (M : matrix n n ð•œ) :=
M.is_hermitian âˆ§ âˆ€ x : n â†’ ð•œ, 0 â‰¤ is_R_or_C.re (dot_product (star x) (M.mul_vec x))

lemma pos_def.pos_semidef {M : matrix n n ð•œ} (hM : M.pos_def) : M.pos_semidef :=
begin
  refine âŸ¨hM.1, _âŸ©,
  intros x,
  by_cases hx : x = 0,
  { simp only [hx, zero_dot_product, star_zero, is_R_or_C.zero_re'] },
  { exact le_of_lt (hM.2 x hx) }
end

lemma pos_semidef.is_hermitian {M : matrix n n ð•œ} (hM : M.pos_semidef) : M.is_hermitian := hM.1

lemma pos_semidef.transpose {M : matrix n n ð•œ} (hM : M.pos_semidef) : Máµ€.pos_semidef :=
begin
  refine âŸ¨is_hermitian.transpose hM.1, Î» x, _âŸ©,
  convert hM.2 (star x) using 2,
  rw [mul_vec_transpose, matrix.dot_product_mul_vec, star_star, dot_product_comm]
end

lemma pos_semidef_diagonal [decidable_eq n] {f : n â†’ â„} (hf : âˆ€ i, 0 â‰¤ f i) :
  (diagonal f).pos_semidef :=
begin
  refine âŸ¨is_hermitian_diagonal _, _âŸ©,
  intro x,
  simp only [star, id.def, is_R_or_C.re_to_real],
  apply finset.sum_nonneg',
  intro i,
  rw [mul_vec_diagonal f x i, mul_comm, mul_assoc],
  exact mul_nonneg (hf i) (mul_self_nonneg (x i))
end

lemma pos_def_diagonal [decidable_eq n] {f : n â†’ â„} (hf : âˆ€ i, 0 < f i) :
  (diagonal f).pos_def :=
begin
  refine âŸ¨is_hermitian_diagonal _, _âŸ©,
  intros x hx,
  simp only [star, id.def, is_R_or_C.re_to_real],
  apply finset.sum_pos',
  { intros i _,
    rw [mul_vec_diagonal f x i, mul_comm, mul_assoc],
    exact mul_nonneg (le_of_lt (hf i)) (mul_self_nonneg (x i)) },
  { contrapose! hx,
    ext i,
    have := hx i (finset.mem_univ _),
    rw [mul_vec_diagonal f x i, mul_comm, mul_assoc] at this,
    have := nonpos_of_mul_nonpos_right this (hf i),
    rw mul_self_eq_zero.1 (le_antisymm this (mul_self_nonneg (x i))),
    refl }
end

-- instance : nontrivial ð•œ := by apply_instance--infinite.nontrivial ð•œ
instance : is_domain ð•œ := by apply_instance


-- Replace? seems to have fewer assumptions than `eq_zero_of_mul_vec_eq_zero`
lemma eq_zero_of_mul_vec_eq_zero' {R : Type*} [comm_ring R] [decidable_eq n]
  {M : matrix n n R} (hM : is_unit M.det) (x : n â†’ R) (h : M.mul_vec x = 0) : x = 0 :=
calc
  x = (Mâ»Â¹ â¬ M).mul_vec x : by rw [nonsing_inv_mul M hM, one_mul_vec]
  ... = 0 : by rw [â† mul_vec_mul_vec, h, mul_vec_zero]

lemma pos_def.det_ne_zero [decidable_eq n] {M : matrix n n ð•œ} (hM : M.pos_def) : M.det â‰  0 :=
begin
  rw â† matrix.nondegenerate_iff_det_ne_zero,
  intros v hv,
  have hv' := hv (star v),
  rw [â† star_eq_zero],
  by_contra h,
  have := hM.2 (star v) h,
  rw [star_star, hv'] at this,
  simpa using this,
end

lemma is_hermitian.nonsingular_inv [decidable_eq n] {M : matrix n n ð•œ}
  (hM : M.is_hermitian) (hMdet : is_unit M.det):
  Mâ»Â¹.is_hermitian :=
begin
  refine (matrix.inv_eq_right_inv _).symm,
  rw [conj_transpose_nonsing_inv, hM.eq, mul_nonsing_inv _ hMdet]
end

lemma pos_def.nonsingular_inv [decidable_eq n] {M : matrix n n ð•œ} (hM : M.pos_def) :
  Mâ»Â¹.pos_def :=
begin
  refine âŸ¨is_hermitian.nonsingular_inv hM.1 (is_unit_iff_ne_zero.2 hM.det_ne_zero), _âŸ©,
  intros x hx,
  have hMMinv := (mul_nonsing_inv _ (is_unit_iff_ne_zero.2 hM.det_ne_zero)),
  have hMinvdet : Mâ»Â¹.det â‰  0 := det_ne_zero_of_left_inverse hMMinv,
  have := hM.2 (Mâ»Â¹.mul_vec x) (Î» h, hx (eq_zero_of_mul_vec_eq_zero hMinvdet h)),
  rw [mul_vec_mul_vec, hMMinv, one_mul_vec, star_dot_product] at this,
  rw [â† is_R_or_C.conj_re],
  exact this
end

-- TODO: move
lemma is_hermitian.conj_transpose_mul_mul (M N : matrix n n ð•œ) (hM : M.is_hermitian) :
  (Ná´´ â¬ M â¬ N).is_hermitian :=
by simp [is_hermitian, hM.eq, matrix.mul_assoc]

lemma pos_def.conj_transpose_mul_mul [decidable_eq n]
    (M N : matrix n n ð•œ) (hM : M.pos_def) (hN : N.det â‰  0):
  (Ná´´ â¬ M â¬ N).pos_def :=
begin
  refine âŸ¨hM.1.conj_transpose_mul_mul M N, _âŸ©,
  intros x hx,
  convert hM.2 (N.mul_vec x) (Î» h, hx (eq_zero_of_mul_vec_eq_zero hN h)) using 2,
  rw [matrix.mul_assoc, mul_vec_mul_vec, â†mul_vec_mul_vec, dot_product_mul_vec, star_mul_vec]
end

lemma pos_semidef.conj_transpose_mul_mul (M N : matrix n n ð•œ) (hM : M.pos_semidef) :
  (Ná´´ â¬ M â¬ N).pos_semidef :=
begin
  refine âŸ¨hM.1.conj_transpose_mul_mul M N, _âŸ©,
  intro x,
  convert hM.2 (N.mul_vec x) using 2,
  rw [matrix.mul_assoc, mul_vec_mul_vec, â†mul_vec_mul_vec, dot_product_mul_vec, star_mul_vec]
end

lemma pos_semidef.mul_mul_of_is_hermitian {M N : matrix n n ð•œ}
    (hM : M.pos_semidef) (hN : N.is_hermitian) :
  (N â¬ M â¬ N).pos_semidef :=
by { convert hM.conj_transpose_mul_mul M N, exact hN.symm }

lemma pos_semidef.submatrix {M : matrix n n ð•œ} (hM : M.pos_semidef) (e : m â‰ƒ n):
  (M.submatrix e e).pos_semidef :=
begin
  refine âŸ¨hM.1.submatrix e, Î» x, _âŸ©,
  have : (M.submatrix â‡‘e â‡‘e).mul_vec x = M.mul_vec (Î» (i : n), x (e.symm i)) âˆ˜ e,
  { ext i,
    dsimp only [(âˆ˜), mul_vec, dot_product],
    rw finset.sum_bij' (Î» i _, e i) _ _ (Î» i _, e.symm i);
    simp only [eq_self_iff_true, implies_true_iff, equiv.symm_apply_apply, finset.mem_univ,
      submatrix_apply, equiv.apply_symm_apply] },
  rw this,
  convert hM.2 (Î» i, x (e.symm i)) using 3,
  unfold dot_product,
  rw [finset.sum_bij' (Î» i _, e i) _ _ (Î» i _, e.symm i)];
  simp only [eq_self_iff_true, implies_true_iff, equiv.symm_apply_apply, finset.mem_univ,
    submatrix_apply, equiv.apply_symm_apply, pi.star_apply],
end

@[simp] lemma pos_semidef_submatrix_equiv {M : matrix n n ð•œ} (e : m â‰ƒ n) :
  (M.submatrix e e).pos_semidef â†” M.pos_semidef :=
âŸ¨Î» h, by simpa using h.submatrix e.symm, Î» h, h.submatrix _âŸ©

lemma pos_semidef.add {M N: matrix n n ð•œ} (hM : M.pos_semidef) (hN : N.pos_semidef) :
  (M + N).pos_semidef :=
begin
  refine âŸ¨hM.1.add hN.1, Î» x, _âŸ©,
  simp only [add_mul_vec, dot_product_add, map_add],
  apply add_nonneg (hM.2 x) (hN.2 x)
end

namespace pos_def

variables {M : matrix n n â„} (hM : M.pos_def)
include hM

--TODO: use in `det_pos`
lemma eigenvalues_pos [decidable_eq n] (i : n) : 0 < hM.1.eigenvalues i :=
begin
  rw hM.is_hermitian.eigenvalues_eq,
  apply hM.2 _ (Î» h, _),
  have h_det : (hM.is_hermitian.eigenvector_matrix)áµ€.det = 0,
    from matrix.det_eq_zero_of_row_eq_zero i (Î» j, congr_fun h j),
  simpa only [h_det, not_is_unit_zero] using
    is_unit_det_of_invertible hM.is_hermitian.eigenvector_matrixáµ€,
end

noncomputable instance [decidable_eq n] : invertible M :=
invertible_of_is_unit_det M (is_unit_iff_ne_zero.2 hM.det_ne_zero)

end pos_def


namespace pos_semidef

variables {M : matrix n n â„} (hM : M.pos_semidef)
include hM

lemma eigenvalues_nonneg [decidable_eq n] (i : n) : 0 â‰¤ hM.1.eigenvalues i :=
by {rw hM.is_hermitian.eigenvalues_eq, apply hM.2}

lemma det_nonneg [decidable_eq n] : 0 â‰¤ det M :=
begin
  rw [hM.1.det_eq_prod_eigenvalues],
  apply finset.prod_nonneg (Î» i hi, _),
  apply eigenvalues_nonneg,
end

end pos_semidef

end matrix
